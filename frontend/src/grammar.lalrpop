use crate::parser::{Stmt, PrintStmt, AssignStmt, Expr, CompareOp, BinaryExpr, Op};

grammar;

/// A program consisting of many statements.
pub Program: Vec<Stmt> = {
    <s:(Stmt)+> => s
}

/// A statement.
pub Stmt: Stmt = {
    /// An if statement.
    "if" <cond:Comparison> <c:CodeBlock> => Stmt::If {
        comparison: cond,
        code: match c {
            Stmt::Block(v) => v,
            _ => panic!("Code Block must follow if statement!")
        }
    },

    /// A code block.
    <CodeBlock> => <>,

    /// A statement that executes terminal code directly.
    "'" <PlainStr> "'" StmtDelim => Stmt::Bash(<>),

    /// A postfix operator statement that indicates an expression should be printed to console.
    <Expr> "?" => Stmt::Print(PrintStmt {
        value: <>
    }),

    // A print statement
    "print" <Expr> StmtDelim => Stmt::Print(PrintStmt {
        value: <>
    }),

    // An assignment statement
    <i:Ident> "=" <v:Expr> StmtDelim => Stmt::Assign(AssignStmt {
        name: match *i {
            Expr::Ident(i) => i.to_string(),
            _ => unimplemented!()
        },
        value: v
    }),

    // An expression
    <Expr> => Stmt::Expr(<>)
}

/// A code block.
CodeBlock: Stmt = "{" <p:Program> "}" => Stmt::Block(p);

/// Stmt Delimiter.
StmtDelim = ";";

/// An expression.
Expr: Box<Expr> = {
    /// An expression that gathers input from terminal, with an optional message prepended.
    "read" => Box::new(
        Expr::Read
    ),

    // Number expressions
    <l:Expr> "+" <r:Factor> => Box::new(
        Expr::Binary(BinaryExpr { lhs: l, op: Op::Plus, rhs: r })
    ),
    <l:Expr> "-" <r:Factor> => Box::new(
        Expr::Binary(BinaryExpr { lhs: l, op: Op::Minus, rhs: r })
    ),
    Factor,

    // Common types
    Str,
    Ident
};

/// Equality condition.
pub Comparison: Box<Expr> = <l:Expr> <cop:CompareOp> <r:Expr> => Box::new(
    Expr::Comparison {
        lhs: l,
        op: cop,
        rhs: r
    }
);

/// An equality operator
CompareOp: CompareOp = {
    ">" => CompareOp::Greater,
    "<" => CompareOp::Less,
    "==" => CompareOp::Equal,
    ">=" => CompareOp::GreaterEqual,
    "<=" => CompareOp::LessEqual,
};

/// A factor.
Factor: Box<Expr> = {
    <l:Factor> "*" <r:Term> => Box::new(
        Expr::Binary(BinaryExpr { lhs: l, op: Op::Mult, rhs: r })
    ),
    <l:Factor> "/" <r:Term> => Box::new(
        Expr::Binary(BinaryExpr { lhs: l, op: Op::Div, rhs: r })
    ),
    Term
};

/// A term.
Term: Box<Expr> = {
    Num,
    "(" <Expr> ")"
};

/// An ident.
pub Ident: Box<Expr> = r#"[a-z_A-Z]+"# => Box::new(
    Expr::Ident(
        <>
        .to_string()
        .replace("\"", "")
    )
);

/// A numeric literal.
Num: Box<Expr> = {
    r"[0-9.]+" => Box::new(Expr::Num(<>.parse().expect("Couldn't parse number from str!")))
};

/// A string.
pub Str: Box<Expr> = r#""([^"\\\x00-\x1F]|\\(["\\bnfrt/]|u[a-fA-F0-9]{4}))*""# => Box::new(
    Expr::Str(
        <>
        .to_string()
        .replace("\"", "")
    )
);

/// A plain non-expr string that returns `String`.
pub PlainStr: String = r#""([^"\\\x00-\x1F]|\\(["\\bnfrt/]|u[a-fA-F0-9]{4}))*""# => 
    String::from(<>).replace("\"", "");
